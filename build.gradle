plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'maven-publish'
	id("com.gradleup.shadow") version "9.0.0-beta17"
}

group = project.maven_group
version = project.mod_version + "+mc" + project.minecraft_version

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
}

configurations {
	shadowImplementation
	implementation.extendsFrom shadowImplementation
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	shadowImplementation("net.dv8tion:JDA:${project.jda_version}") {
		exclude module: 'opus-java'
		exclude module: 'tink'
	}

	/* JDA Dependencies */
	shadowImplementation('com.fasterxml.jackson.core:jackson-databind:2.13.4.2')
	shadowImplementation('com.fasterxml.jackson.core:jackson-annotations:2.13.4')
	shadowImplementation('com.fasterxml.jackson.core:jackson-core:2.13.4')
	shadowImplementation("org.apache.commons:commons-collections4:4.4")
	shadowImplementation('com.squareup.okio:okio:1.17.2')
	shadowImplementation('com.neovisionaries:nv-websocket-client:2.14')
	shadowImplementation('com.squareup.okhttp3:okhttp:4.12.0')
	shadowImplementation("net.sf.trove4j:core:3.1.0")

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
//	implementation("com.squareup.okhttp3:okhttp:4.12.0")
//	shadowImplementation "com.squareup.okhttp3:okhttp:4.12.0"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand ("version": project.version)
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

tasks.shadowJar {
	configurations = [project.configurations.shadowImplementation]
	enableRelocation = true
	relocationPrefix = "shadow.summit"
	exclude("META-INF")
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
	
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName
	
	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

remapJar {
	dependsOn(shadowJar)
	mustRunAfter(shadowJar)
	inputFile = file(shadowJar.archivePath)
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}